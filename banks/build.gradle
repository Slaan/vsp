/**
 * This is main build script for the whole example application.  * To execute the whole build pipeline run 'gradlew buildAll'
 * from a command line in the directory this file is located in.
 * Hint: Make sure your Docker Daemon is running and listening
 * on the configured IP:PORT (default: 192.168.99.100:2376, can be
 * checked with 'docker-machine url default').
 *
 * File created and maintained by:
 */


/**
 * Define general parameters, dependencies and repositories for the build script
 */
buildscript {
    ext {
        springBootVersion = '1.2.2.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.bmuschko:gradle-docker-plugin:2.6.1")
    }
}

/**
 * Load several plugins, consult the gradle documentation for more info
 */
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'com.bmuschko.docker-remote-api'

// imports for DockerTasks
import org.gradle.internal.os.OperatingSystem;
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPullImage
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.container.DockerRemoveContainer



/**
 * Define the name and version of the resulting jar file
 */
jar {
    baseName = 'banks'
    version = '0.0.1-SNAPSHOT'
}

checkstyle {
  ignoreFailures = false
  configFile = new File(rootDir, "./../doc/codingstyle/checkstyle.xml")
  // Add latest release, since gradle takes a recent version by default
  toolVersion = "6.11.2"
}

/**
 * Define source and target java version compatibility
 */
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'vsp.banks.MyMainClass'

/**
 * Setup library repositories
 */
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        // Can't find spring-cloud-build in maven central...
        url 'http://maven.springframework.org/release'
    }
}

sourceSets {
    serviceTest {
        java.srcDir file('src/serviceTest/java')
        resources.srcDir file('src/serviceTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }

    /*consumerDrivenTest {
        java.srcDir file('src/consumerDrivenTest/java')
        resources.srcDir file('src/consumerDrivenTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }*/
}

configurations {
    serviceTestCompile.extendsFrom testCompile
    serviceTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.mashape.unirest:unirest-java:1.4.7'

    testCompile 'org.mockito:mockito-core:2.0.2-beta'
    testCompile 'org.assertj:assertj-core:3.1.0'
    testCompile('org.testng:testng:6.9.9') {
        exclude group:'junit', module:'junit'
    }
    testCompile 'com.jayway.restassured:rest-assured:2.5.0'
}

/**
 * Setup docker task
 */
docker {
    url = 'https://192.168.99.100:2375'
    if (OperatingSystem.current().isLinux()) {
      url = 'http://127.0.0.1:2375'
    }
    // set path to the certificate directory on your machine
    certPath = new File(System.properties['user.home'],'/.docker/machine/machines/default')

    registryCredentials {
        url = 'https://artifactory.mars.haw-hamburg.de:5002'
        username = '<DeineHAWKennung>'
        password = '<DeinHAWPasswort>'
        email = '<DeineHAWMailAdresse>'
    }

}

/**
 * Setup Tests
 */
test {
    // enable TestNG support (default is JUnit)
    useTestNG()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

/**
 * This task copies the tar file created by the distTar task from
 * the distributions folder to the docker folder. The distTar task
 * in return is called from the build task, which is a dependency
 * of this task.
 */
task copyDist(type: Copy, dependsOn: [build, wrapper]) {
    from('build/distributions/' + jar.baseName + '.tar')
    into('build/docker/')
}

/**
 * This task creates the Dockerfile needed to create the Docker image.
 * It inherits the java:8 docker image, sets some Spring Docker Profile variable,
 * exposes port 8080 to the outer world and adds our tar file as content at the
 * root path of the image.
 */
task createDockerfile(type: Dockerfile, dependsOn: [copyDist]) {
    destFile = project.file('build/docker/Dockerfile')
    from 'java:8'
    maintainer 'Alex Mante "alex.mantel@haw-hamburg.de"'
    environmentVariable('JAVA_OPTS', '-Dspring.profiles.active=docker')
    exposePort(4567)
    entryPoint("/banks/bin/banks")
    // Copy contents from the build output's .tar file into container
    // the ADD command in docker takes a .tar file and extracts it into
    // the given folder in the container. In this case '/'
    addFile({
        jar.baseName + '.tar'
    }, {
        '/'
    })
}

/**
 * This task builds the actual image of this service by using the previously
 * created Dockerfile from the ./build/docker folder and applying a talking tag
 * to the image
 */
task buildImage(type: DockerBuildImage, dependsOn: [createDockerfile]) {
    inputDir = createDockerfile.destFile.parentFile
    tag = 'banks'
}

/**
 * Start and Stop Tasks for ReservationAPI-Server
 */
task createBanksServiceContainer(type: DockerCreateContainer, dependsOn: [buildImage]) {
    imageId = "banks"
    portBindings = ["4567:4567"]
    containerName = "banks"
}

task startBanksServiceContainer(type: DockerStartContainer, dependsOn: [createBanksServiceContainer]) {
    targetContainerId { createBanksServiceContainer.getContainerId() }
}

task removeBanksServiceContainer(type: DockerRemoveContainer) {
    targetContainerId { createBanksServiceContainer.getContainerId() }
}

task stopBanksServiceContainer(type: DockerStopContainer) {
    targetContainerId { createBanksServiceContainer.getContainerId }
    finalizedBy removeBanksServiceContainer
}

task setupApplicationLandscape(dependsOn: [startBanksServiceContainer]){

}
setupApplicationLandscape.doLast {
    println("Waiting banks service to start...")
    sleep(30 * 1000)
    println("Waited milliseconds.")
}

sourceSets {
    serviceTest {
        java.srcDir file('src/serviceTest/java')
        resources.srcDir file('src/serviceTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

/**
 * This task executes all service tests. The supporting Netflix services
 * are required to start our own services, so it is made sure these are available and properly started. 
 */
task serviceTest(type: Test, dependsOn: [setupApplicationLandscape]) {
    useTestNG()
    testClassesDir = sourceSets.serviceTest.output.classesDir
    classpath = sourceSets.serviceTest.runtimeClasspath
    finalizedBy clean
    // finalizedBy stopBanksServiceContainer
}

/**
 * Start this task to kick off the build.
 * It'll build this project and all sup-projects, as well as
 * run the local Unit Tests and create Docker images for each
 * project, if all was well.
 */
task buildAll(dependsOn: [buildImage]){

}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        exceptionFormat 'full'
    }
}
